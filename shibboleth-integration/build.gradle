apply plugin: 'java'

def vhProperties = new Properties()
vhProperties.load(file('../virtualhome/application.properties').newInputStream())

version = vhProperties.get('app.version')
archivesBaseName = 'vh-shib-integration'

sourceSets {
  main {
    java {
      srcDirs = ['src']
    }
  }
}

repositories {
  mavenCentral()
  maven {
    url 'https://build.shibboleth.net/nexus/content/repositories/releases/'
  }
}

dependencies {
  // javax.servlet < 3.0 doesn't have ServletRequest.getServletContext()
  compile 'org.apache.tomcat:tomcat-servlet-api:7.0.42'

  [
    'edu.internet2.middleware:shibboleth-identityprovider:2.4.0',
    'org.opensaml:opensaml:2.6.0',
    'org.opensaml:openws:1.5.0',
    'org.opensaml:xmltooling:1.4.0'
  ].each {
    compile(it) {
      transitive = false
    }
  }

  compile 'commons-codec:commons-codec:1.8'
  compile 'org.apache.httpcomponents:httpcore:4.1'
  compile 'org.apache.httpcomponents:httpclient:4.2.5'
  compile 'com.googlecode.json-simple:json-simple:1.1'
  compile 'org.slf4j:slf4j-api:1.7.5'
}
